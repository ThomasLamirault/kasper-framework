package com.viadeo.kasper.gradle;

import org.gradle.api.Plugin;
import org.gradle.api.Project;
import org.gradle.api.Task;

class KasperHadoopExtension {
    def String pigSourceDir = 'src/main/pig'
    def String hiveSourceDir = 'src/main/hive'
    def String avroSchemasDir = 'src/main/avro'
    def String avroSourceDir = 'generated-sources/avro'
    def String debianRootDir = 'usr/local/'
    def String debianDescription = 'Package generated by kasper-hadoop for project '
}

class KasperHadoop implements Plugin<Project> {
    void apply(Project project) {
        project.extensions.create("kasperHadoopConf", KasperHadoopExtension)
        project.kasperHadoopConf.debianRootDir += project.name
        project.kasperHadoopConf.debianDescription += project.name

        //- Dependency to Kasper hadoop packaging------------------------------
        project.task(type: KasperHadoopPackageTask, 'kasperHadoopPackage')
        project.assemble.doLast {
            project.tasks.kasperHadoopPackage.execute()
        }        

        //- Kasper hadoop test dependencies -----------------------------------
        project.task(type: KasperHadoopDependenciesTask, 'kasperHadoopDependencies')
        project.check.doFirst {
            project.tasks.kasperHadoopDependencies.execute()
        }

        //- Dependency to AVRO plugin -----------------------------------------
        project.apply plugin: 'avro-gradle-plugin'
        project.compileAvro {
            source = project.kasperHadoopConf.avroSchemasDir
            destinationDir = project.file(project.kasperHadoopConf.avroSourceDir)
        }
        project.sourceSets {
            main {
                java {
                    srcDir project.compileAvro.destinationDir
                }
            }
        }
        project.dependencies {
            project.compileAvro
        }

        //- Kasper hadoop configurations --------------------------------------
        project.configurations {
            pigRuntime {
                extendsFrom testRuntime
            }
            hiveRuntime {
                extendsFrom testRuntime
            }
        }

        //- Kasper hadoop source sets -----------------------------------------
        project.sourceSets {
            main {
                resources {
                    srcDir project.kasperHadoopConf.pigSourceDir
                    srcDir project.kasperHadoopConf.hiveSourceDir
                }
            }
        }

        //- Dependency hacks --------------------------------------------------
        project.dependencies {
            compile("com.google.protobuf:protobuf-java:2.4.0a") {
                force = true
            }
        }

    }
}

